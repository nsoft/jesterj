import java.text.SimpleDateFormat

/*
 * Copyright 2013-2017 Needham Software LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  repositories {
    maven {
      credentials {
        username = project.hasProperty("jj_artifactory_user") ? project.getProperty("jj_artifactory_user") : System.env.ARTIFACTORY_USER
        password = project.hasProperty("jj_artifactory_password") ? project.getProperty("jj_artifactory_password") : System.env.ARTIFACTORY_PASSWORD
      }
      url 'https://jesterj.jfrog.io/jesterj/libs-release/'
    }
    flatDir {
      dir 'onejar'
    }

  }
  dependencies {
    //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    classpath 'com.github.rholder:gradle-one-jar:1.0.4'
  }
}

plugins {
  id 'com.github.jk1.dependency-license-report' version '0.3.4'
}

allprojects {
  apply plugin: "com.jfrog.artifactory"
}

artifactory {
  contextUrl = "https://jesterj.jfrog.io/jesterj"   //The base Artifactory URL if not overridden by the publisher/resolver
  publish {
    repository {
      repoKey = 'libs-snapshot-local'
      /*

        System env var is for Travis CI, add these to gradle.properties for local builds

        jj_artifactory_user=anonymous
        jj_artifactory_password=

        Note that anonymous user will not be able to test out new versions of libraries. Use mavenLocal() for that.

       */
      username = project.hasProperty("jj_artifactory_user") ? project.getProperty("jj_artifactory_user") : System.env.ARTIFACTORY_USER
      password = project.hasProperty("jj_artifactory_password") ? project.getProperty("jj_artifactory_password") : System.env.ARTIFACTORY_PASSWORD
      maven = true

    }
    defaults {
      publications('mavenJava')
      publishArtifacts = true
      publishPom = true
    }
  }
  resolve {
    repository {
      repoKey = 'libs-release'
      username = project.hasProperty("jj_artifactory_user") ? project.getProperty("jj_artifactory_user") : System.env.ARTIFACTORY_USER
      password = project.hasProperty("jj_artifactory_password") ? project.getProperty("jj_artifactory_password") : System.env.ARTIFACTORY_PASSWORD
      maven = true

    }
  }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'gradle-one-jar'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

licenseReport {
  excludeGroups = ['jini']
}

repositories {
  maven {
    url 'https://clojars.org/repo'
  }
  maven {
    credentials {
      username = propOrDefault("jj_artifactory_user", System.env.ARTIFACTORY_USER)
      password = propOrDefault("jj_artifactory_password", System.env.ARTIFACTORY_PASSWORD)
    }
    url 'https://jesterj.jfrog.io/jesterj/libs-release/'
  }
  flatDir {
    dir 'onejar'
  }
}

configurations {
  oneJarLib
  oneJarPack
  // under no circumstances shall we distribute these in our archives
  oneJarPack.exclude group: 'junit'               // test lib
  oneJarPack.exclude group: 'org.easymock'        // test lib
  // Conflicting licenses...
  oneJarPack.exclude group: 'org.jboss.logging'
  oneJarPack.exclude group: 'com.google.code.findbugs', module: 'annotations' // N.B. findbugs jsr305 is ok, thus module
  oneJarPack.exclude group: 'c3p0', module: 'c3p0'
  oneJarPack.extendsFrom runtime
}

//////////////////////////

 version = '1.0-beta1'
 group = 'org.jesterj'

//////////////////////////

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId 'jesterj-ingest'
      version project.version
      from components.java
      artifact oneJar {
        classifier "node"
      }
    }
  }
}


dependencies {
  compile 'org.apache.tika:tika-parsers:1.16'
  compile 'com.mchange:c3p0:0.9.5.2' // for tika deps... actual declared dep includes older LGPL version only.

  compile 'org.apache.solr:solr-solrj:5.5.0'
  compile 'org.elasticsearch:elasticsearch:2.3.1'

  compile 'com.google.guava:guava:18.0'
  compile 'com.google.code.findbugs:jsr305:3.0.0'

  compile 'jini:jini-core:2.1'
  compile 'org.apache.river:reggie:2.2.2'
  compile 'org.apache.cassandra:cassandra-all:3.11.0-JJ'
  compile ('com.datastax.cassandra:cassandra-driver-core:3.2.0')
  compile 'org.yaml:snakeyaml:1.14'
  compile 'org.apache.velocity:velocity:1.7'

  compile 'org.apache.logging.log4j:log4j-core:2.6'
  compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.6'
  compile 'org.apache.logging.log4j:log4j-jcl:2.6'
  compile 'org.apache.logging.log4j:log4j-1.2-api:2.6'
  compile 'org.apache.logging.log4j:log4j-jul:2.6'

  compile('com.needhamsoftware:easier-mock:2.0')
  compile 'net.jini:jsk-platform:2.2.2'
  compile 'docopt:docopt:0.6.1'

  testCompile 'junit:junit:4.11'
  testCompile 'com.needhamsoftware:easier-mock:2.0'
  //runtime ':one-jar-boot:0.98' // uncomment for debugging stack traces into one-jar classes

  oneJarLib files('onejar/one-jar-boot-0.98.jar')
  compile files('onejar/one-jar-boot-0.98.jar')
  compile('org.reflections:reflections:0.9.10') {
    exclude group: 'org.javassist'
  }
  compile 'org.javassist:javassist:3.20.0-GA' // substitute a version that handles java 8

  // this shouldn't be necessary but there's something dodgy going on with jacocoTestReport's classpath
  // using testCompile so we don't package these...
  testCompile (group: 'org.jacoco', name:'org.jacoco.report', version:'0.7.7.201606060606')
  testCompile (group: 'org.jacoco', name:'org.jacoco.core', version:'0.7.7.201606060606')
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
  jacocoClasspath += files {configurations.testCompile.resolve()}
}

jar {
  archiveName = 'jesterj-ingest-'+project.version+'.jar'
}

task oneJar(type: OneJar) {
  mainClass = 'org.jesterj.ingest.Main'
  archiveName = 'jesterj-node-'+project.version+'.jar'
  useStable = false
  //noinspection GroovyAssignabilityCheck
  manifest {
    //noinspection GroovyAssignabilityCheck
    attributes 'One-Jar-URL-Factory': 'com.simontuffs.onejar.JarClassLoader$OneJarURLFactory'
  }
  oneJarConfiguration = configurations.oneJarLib
  targetConfiguration = configurations.oneJarPack
  doLast{
    def dir = file('licenses').parent
    ant.jar(update:true, destFile: file("build/libs/"+archiveName) ) {
      fileset(dir: dir) {
        include(name: 'licenses/**')
        include(name: 'NOTICE.txt')
        include(name: 'LICENSE.txt')
      }
    }
  }
}

task generateNotices() {
  doLast {
    def toConcatenate = fileTree(file('licenses')) {
      include '**/Apache-2.0/*/NOTICE.txt'
    }
    def header = new File("notice-header.txt")
    def output = new File("NOTICE.txt")
    output.write('') // truncate file
    output << header.text
    toConcatenate.each { f ->
      output << "\n\n-------------------------------------------------------------------------------\n"
      output << "       NOTICE for " + (file(f.parent).name)
      output << "\n-------------------------------------------------------------------------------\n"

      output << f.text
    }
  }
}

jacocoTestReport.dependsOn test

artifactoryPublish.dependsOn oneJar

/*

 Maven Publishing stuff to publish to central. This also allows you to publish
 to a local repository on your system byt setting the version to -LOCAL instead
 of -SNAPSHOT. Specifically:

  To test pom and artifact generation locally...

  1. Ensure GPG keyring in .gnupg dir
  2. Check that the value (above) for ext.
  3. Change version to remove -SNAPSHOT and add -LOCAL
  4. Run gradle uploadArchives
  5. observe archives uploaded to ext.uploadRepo (adjust location as needed)


  To release to central

  1. Ensure GPG keyring in .gnupg dir
  2. Ensure passwords for sonatype in gradle.properties
  3. Test the production of artifacts locally with -LOCAL (see above)
  4. No really, test it, and read everything...
  5. Seriously, don't skip #3!
  6. Change version to remove -SNAPSHOT
  7. Run gradle uploadRelease
  8. Hope to hell you didn't miss anything in step 3....
  9. you skipped #3? OMG you suck... *sigh*

 */

ext.isSnapshot = version.endsWith('-SNAPSHOT')
ext.isLocal = version.endsWith('-LOCAL')
ext.isRelease = !(isLocal || isSnapshot)

ext.releaseRepo = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
ext.snapshotRepo = 'https://oss.sonatype.org/content/repositories/snapshots'
ext.testingRepo = 'file:///tmp/myRepo/'
ext.uploadRepo = isRelease ? releaseRepo : (isSnapshot ? snapshotRepo : testingRepo)


task uploadSnapshot(dependsOn: uploadArchives) << {}
task uploadRelease(dependsOn: uploadArchives) << {}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
  archiveName = 'jesterj-ingest-'+project.version+'-sources.jar'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
  archiveName = 'jesterj-ingest-'+project.version+'-javadoc.jar'
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

// A little idiot proofing...
task checkUploadDest {
  doLast {
    if (gradle.taskGraph.hasTask(':uploadRelease')) {
      if (isSnapshot) {
        ant.fail('Release should not have suffix of -SNAPSHOT')
      }
      if (isLocal) {
        ant.fail('Release should not have suffix of -LOCAL')
      }
    } else if (gradle.taskGraph.hasTask(':uploadSnapshot')) {
      if (!isSnapshot) {
        ant.fail('Snapshot should have suffix of SNAPSHOT')
      }
    } else {
      if (!isLocal) {
        ant.fail('This is not a local version, ' +
            'please use either uploadRelease or uploadSnapshot, or change the version suffix to -LOCAL')
      }
    }
  }
}

// Uncomment this to do sonatype uploads (fails on all other machines looking for gpg key otherwise
//
signing {
    required { !isLocalVersion && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}


ext.isLocalVersion = !version.endsWith("LOCAL")

uploadArchives {
  it.dependsOn checkUploadDest
  repositories {
    mavenDeployer {
      beforeDeployment { deployment -> signing.signPom(deployment) }
      repository(url: project.uploadRepo) {
        authentication(userName: propOrDefault('sonatypeUsername','foo'), password: propOrDefault('sonatypePassword','foo'))
      }
      pom.project {
        parent {
          groupId 'org.sonatype.oss'
          artifactId 'oss-parent'
          //noinspection GroovyAssignabilityCheck
          version '7'
        }
        artifactId 'jesterj-ingest'
        name 'JesterJ Ingester'
        description 'Document ingestion for search'
        url 'https://github.com/nsoft/jesterj'
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
        scm {
          connection 'scm:git:git@github.com:nsoft/jesterj.git'
          developerConnection 'scm:git:git@github.com:nsoft/jesterj.git'
          url 'git@github.com:nsoft/jesterj.git'
        }
        developers {
          developer {
            id 'nsoft'
            name 'Patrick Heck'
            email 'gus@needhamsoftware.com'
          }
        }
      }
      pom.withXml { root ->
        def children = root.asNode().children()
        def versionIndex = children.indexOf(children.find { it.name().localPart == 'version' })
        // Stuff that sonatype wants... there may be a way to make these appear automatically but...
        // http://issues.gradle.org/browse/GRADLE-1285
        children.add(versionIndex + 1, new Node(null, 'packaging', 'jar'))
      }
    }
  }
}

jar.manifest.attributes getManifestAttributes()
sourcesJar.manifest.attributes getManifestAttributes()
javadocJar.manifest.attributes getManifestAttributes()

@SuppressWarnings("GroovyAssignabilityCheck")
def getManifestAttributes() {
  Map<String, String> attrs = new LinkedHashMap<>();
  attrs.put('Implementation-Title', project.name)
  attrs.put('Implementation-Version', version)
  attrs.put('Build-Timestamp', getTimestamp())
  attrs.put('Build-Jdk', getJdk())
  attrs.put('Build-Tool', GradleVersion.current())
  return attrs
}

def getTimestamp() {
  def dt = new Date()
  def df = new SimpleDateFormat('yyyy-MM-dd HH:mm:ss', Locale.ENGLISH)
  df.format(dt)
}

def getJdk() {
  "${System.getProperty('java.version')} (${System.getProperty('java.vm.vendor')} ${System.getProperty('java.vm.version')})"
}

def propOrDefault(String prop, String defaultVal) {
  return project.hasProperty(prop) ? project.getProperty(prop) : defaultVal
}
